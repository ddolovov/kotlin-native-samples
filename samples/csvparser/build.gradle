import org.jetbrains.kotlin.konan.target.*

plugins {
    id 'kotlin-multiplatform' version '1.3-SNAPSHOT'
}
repositories {
    maven { url 'http://oss.sonatype.org/content/repositories/snapshots' }
    mavenCentral()
}
kotlin {
    targets {
        fromPreset(presets.findByName(KonanTargetKt.getPresetName(HostManager.host)), 'csvParser')
        configure([csvParser]) {
            compilations.main.outputKinds('EXECUTABLE')
            compilations.main.entryPoint = 'sample.csvparser.main'
        }
    }
}

task runProgram {
    def buildType = 'release' // Change to "debug" to run application with debug symbols.
    dependsOn "link${buildType.capitalize()}ExecutableCsvParser"
    doLast {
        def programFile = kotlin.targets.csvParser.compilations.main.getBinary("EXECUTABLE", buildType)
        exec {
            executable programFile
            args = ['./European_Mammals_Red_List_Nov_2009.csv', '4', '100']
        }
    }
}