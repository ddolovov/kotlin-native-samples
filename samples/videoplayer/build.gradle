plugins {
    id 'kotlin-multiplatform'
}

kotlin {
    presets {
        // Determine host preset.
        MPPTools.defaultHostPreset(this, [macosX64, linuxX64, mingwX64])
    }

    targets {
        fromPreset(hostPreset, 'videoPlayer') {
            compilations.main.outputKinds('EXECUTABLE')
            compilations.main.entryPoint = 'sample.videoplayer.main'
            
            switch (hostPreset) {
                case presets.macosX64:
                    compilations.main.linkerOpts "-F ${System.getProperty("user.home")}/Library/Frameworks",
                            "-F /Library/Frameworks",
                            "-L/opt/local/lib",
                            "-L/usr/local/lib"
                    break
                case presets.linuxX64:
                    compilations.main.linkerOpts '-L/usr/lib/x86_64-linux-gnu', '-L/usr/lib64'
                    break
                case presets.mingwX64:
                    compilations.main.linkerOpts '-LC:/msys64/mingw64/lib'
                    break
            }
            
            compilations.main.cinterops {
                ffmpeg {
                    defFile 'src/videoPlayerMain/c_interop/ffmpeg.def'
                    switch (hostPreset) {
                        case presets.macosX64: 
                            includeDirs.headerFilterOnly '/opt/local/include', '/usr/local/include'
                            break
                        case presets.linuxX64:
                            includeDirs.headerFilterOnly '/usr/include', '/usr/include/x86_64-linux-gnu', '/usr/include/ffmpeg'
                            break
                        case presets.mingwX64:
                            includeDirs 'C:/msys64/mingw64/include'
                            break
                    }
                }
                sdl {
                    defFile 'src/videoPlayerMain/c_interop/sdl.def'
                    switch (hostPreset) {
                        case presets.macosX64:
                            includeDirs '/Library/Frameworks/SDL2.framework/Headers',
                                    "${System.getProperty("user.home")}/Library/Frameworks/SDL2.framework/Headers",
                                    '/opt/local/include/SDL2',
                                    '/usr/local/include/SDL2'
                            break
                        case presets.linuxX64:
                            includeDirs '/usr/include/SDL2'
                            break
                        case presets.mingwX64:
                            includeDirs 'C:/msys64/mingw64/include/SDL2'
                            break
                    }
                }
            }
        }
    }
}
