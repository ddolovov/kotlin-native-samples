// Reuse Kotlin version from the root project.
def rootProjectGradlePropertiesFile = file("${rootProject.projectDir}/../gradle.properties")
if (!rootProjectGradlePropertiesFile.isFile()) {
    throw new Exception("File $rootProjectGradlePropertiesFile does not exist or is not a file")
}

def kotlinVersion = null
rootProjectGradlePropertiesFile.withInputStream { inputStream ->
    def properties = new Properties()
    properties.load(inputStream)

    kotlinVersion = properties.getProperty('kotlin_version')
    if (kotlinVersion == null) {
        throw new Exception("No 'kotlin_version' property in $rootProjectGradlePropertiesFile file")
    }
}

gradle.beforeProject { project ->
    project.ext.kotlin_version = kotlinVersion
}
